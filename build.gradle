/*
   Project jacamo_web_demo

   Gradle build file for JaCaMo Applications
   June 21, 2020 - 03:07:00
*/

plugins {
  id "java-library"
  id "eclipse"
  id "com.github.johnrengelman.shadow" version "5.2.0"
}

defaultTasks 'run'

version '1.0'
group   'org.jacamo'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    maven { url "http://jacamo.sourceforge.net/maven2" }
    maven { url "https://raw.github.com/jacamo-lang/mvn-repo/master" }
    maven { url "https://repo.gradle.org/gradle/libs-releases-local" }
    mavenCentral()
    flatDir { dirs 'lib' }
}

dependencies {
    compile group: 'org.jacamo', name: 'jacamo', version: '0.9-SNAPSHOT'
    compile group: 'org.jacamo', name: 'jacamo-web', version: '0.5-SNAPSHOT'
}

sourceSets {
    main {
        java {
            srcDir 'src/env'
            srcDir 'src/agt'
            srcDir 'src/org'
            srcDir 'src/java'
        }
        resources {
            srcDir 'src/resources'
        }
    }
}

task run (type: JavaExec, dependsOn: 'classes') {
    group       ' JaCaMo'
    description 'runs the JaCaMo application'
    doFirst {
        mkdir 'log'
    }
    main 'jacamo.infra.JaCaMoLauncher'
    args 'marketplace.jcm'
    classpath sourceSets.main.runtimeClasspath
}

clean {
    delete 'bin'
    delete 'build'
    delete 'log'
}

/*
 *
 * Heroku support
 *
 */

task copyToLib(type: Copy) {
    into "$buildDir/libs"
    from(configurations.compile)
}

task stage(dependsOn: ['shadowJar', 'copyToLib', 'clean']) {
	copyToLib.mustRunAfter clean
	jar.mustRunAfter copyToLib
}

stage.dependsOn(copyToLib)

jar {
    baseName 'jacamo-web-demo'
    manifest {
      attributes(
        'Class-Path': configurations.compile.collect { it.getName() }.join(' '),
        'Main-Class': 'jacamo.infra.JaCaMoLauncher',
        'args': 'marketplace.jcm'
      )
    }
}


/*
 *
 * end of file
 *
 */
